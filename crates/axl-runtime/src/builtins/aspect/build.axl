"""
A default 'build' task that wraps a 'bazel build' command.
"""

BuildConfig = spec(
    startup_flags = attr(typing.Callable[[list[str]], list[str]], lambda flags: flags),
    flags = attr(typing.Callable[[list[str]], list[str]], lambda flags: flags),
    build_event_sinks = attr(typing.Callable[[], list[bazel.build.BuildEventSink]], lambda: []),
    # TODO: build event sinks configuration
    # Lifecycle
    build_start = attr(typing.Callable[[], None], lambda: None),
    build_end = attr(typing.Callable[[int], None], lambda status: None)
)

def impl(ctx: TaskContext) -> int:
    stdout = ctx.std.io.stdout

    build_events = True
    for bes_backend in ctx.args.bes_backend:
        metadata = {}
        for bes_header in ctx.args.bes_header:
            (k, _, v) = bes_header.partition("=")
            metadata[k] = v
        if type(build_events) != "list":
            build_events = []
        build_events.append(
            bazel.build_events.grpc(
                uri = bes_backend,
                metadata = metadata
            )
        )

    extra_sinks = ctx.config.build_event_sinks()
    if extra_sinks:
        if type(build_events) != "list":
            build_events = []
        build_events.extend(extra_sinks)

    bazel_flags = ["--isatty=" + str(int(ctx.std.io.stdout.is_tty))]
    for bazel_flag in ctx.args.bazel_flag:
        bazel_flags.append(bazel_flag)

    bazel_startup_flags = []
    for flag in ctx.args.bazel_startup_flag:
        bazel_startup_flags.append(flag)

    # Apply flags and startup flags by config
    bazel_flags = ctx.config.flags(bazel_flags)
    bazel_startup_flags = ctx.config.startup_flags(bazel_startup_flags)

    ctx.config.build_start()

    build = ctx.bazel.build(
        build_events = build_events,
        flags = bazel_flags,
        startup_flags = bazel_startup_flags,
        *ctx.args.target_pattern
    )

    build_status = build.wait()

    ctx.config.build_end(build_status.code)

    return build_status.code

build = task(
    implementation = impl,
    config = BuildConfig,
    args = {
        # TODO: Support a long --pattern_file like bazel does (@./targets)
        # TODO: Support - (list from stdin)
        "target_pattern": args.positional(minimum = 1, maximum = 512, default = ["..."]),
        "bazel_flag": args.string_list(),
        "bazel_startup_flag": args.string_list(),
        "remote_executor": args.string(),
        "remote_cache": args.string(),
        "bes_backend": args.string_list(),
        "bes_header": args.string_list(),
    }
)
