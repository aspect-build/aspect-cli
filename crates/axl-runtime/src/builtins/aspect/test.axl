"""
A default 'test' task that wraps a 'bazel test' command.
"""

def _test_impl(ctx):
    stdout = ctx.std.io.stdout

    build_events = True
    for bes_backend in ctx.args.bes_backend:
        metadata = {}
        for bes_header in ctx.args.bes_header:
            (k, _, v) = bes_header.partition("=")
            metadata[k] = v
        if type(build_events) != "list":
            build_events = []
        build_events.append(
            bazel.build_events.grpc(
                uri = bes_backend,
                metadata = metadata
            )
        )

    bazel_flags = ["--isatty=" + str(int(ctx.std.io.stdout.is_tty))]
    for flag in ctx.args.bazel_flag:
        bazel_flags.append(flag)

    bazel_startup_flags = []
    for flag in ctx.args.bazel_startup_flag:
        bazel_startup_flags.append(flag)

    test = ctx.bazel.test(
        build_events = build_events,
        flags = bazel_flags,
        startup_flags = bazel_startup_flags,
        *ctx.args.target_pattern
    )

    build_status = test.wait()
    return build_status.code

test = task(
    implementation = _test_impl,
    args = {
        # TODO: Support a long --pattern_file like bazel does (@./targets)
        # TODO: Support - (list from stdin)
        "target_pattern": args.positional(minimum = 1, maximum = 512, default = ["..."]),
        "bazel_flag": args.string_list(),
        "bazel_startup_flag": args.string_list(),
        "bes_backend": args.string_list(),
        "bes_header": args.string_list()
    }
)
