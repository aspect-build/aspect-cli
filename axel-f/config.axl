"""Workflows Configuration"""

load("./configure.axl", "configure")
load("./gazelle.axl", "gazelle")
load("./buildifier.axl", "buildifier")
load("./migrate.axl", "migrate")
load("./delivery.axl", "delivery")
load("./marvinecho.axl", "marvinecho")
load("./platform-config.axl",
    "read_platform_config",
    "read_host_config",
    "get_bazelrc_flags",
    "DEFAULT_PLATFORM_DIR"
)

def _check_deliveryd_health(ctx, socket_path):
    """
    Check if deliveryd is healthy by calling the /health endpoint.

    Returns:
        True if healthy, False otherwise
    """
    http = ctx.http()
    response = http.get(
        url="http://localhost/health",
        unix_socket=socket_path,
    )

    result = response.map_err(lambda e: str(e)).block()
    if type(result) == "string":
        return False
    return result.status >= 200 and result.status < 300

def _start_deliveryd(ctx, delivery_db_endpoint, socket_path = "/tmp/deliveryd.sock"):
    """
    Start the deliveryd process in the background if not already running.

    Args:
        ctx: Config context
        delivery_db_endpoint: Redis endpoint URL (redis:// or rediss:// for TLS)
        socket_path: Unix socket path for deliveryd
    """
    # Check if deliveryd is already running by checking for the socket
    if ctx.std.fs.exists(socket_path):
        # Socket exists, check if the instance is healthy
        if _check_deliveryd_health(ctx, socket_path):
            print("deliveryd already running and healthy (socket: {})".format(socket_path))
            return
        else:
            # Stale socket, remove it
            print("deliveryd socket exists but instance is unhealthy, removing stale socket")
            ctx.std.fs.remove_file(socket_path)

    cmd = ctx.std.process.command("deliveryd")
    cmd.arg("--socket=" + socket_path)
    cmd.arg("--redis-endpoint=" + delivery_db_endpoint)

    # Run in background
    cmd.stdout("null")
    cmd.stderr("null")
    cmd.spawn()

    print("Started deliveryd (socket: {}, endpoint: {})".format(socket_path, delivery_db_endpoint))

def config(ctx: ConfigContext):
    ctx.tasks.add(configure)
    ctx.tasks.add(gazelle)
    ctx.tasks.add(buildifier)
    ctx.tasks.add(migrate)
    ctx.tasks.add(delivery)
    ctx.tasks.add(marvinecho)

    CI = ctx.std.env.var("BUILDKITE")

    if CI:
        print("--- :aspect-build: Configuring Workflows")

    # Read platform config from disk
    platform_config = read_platform_config(ctx.std.fs)

    # Start deliveryd if delivery_db_endpoint is configured
    delivery_db_endpoint = platform_config.get("delivery_db_endpoint")
    if delivery_db_endpoint:
        _start_deliveryd(ctx, delivery_db_endpoint)

    # Read host config from environment
    host_config = read_host_config(ctx.std.env, ctx.std.io)

    # Generate bazelrc content
    flags = get_bazelrc_flags(
        platform_config = platform_config,
        host_config = host_config,
        bazel_version = "7.0.0",
    )

    # Debugging information
    if CI:
        print(platform_config)
        print(host_config)
        print(flags)

    user = ctx.std.env.var("USER")

    def flags(flags, type):
        if user != "thesayyn" and type in flags:
            flags += flags[type]
        return flags

    bessie_endpoint = platform_config.get("bessie_endpoint", None)
    def build_event_sinks():
        sinks = []
        if user != "thesayyn" and bessie_endpoint:
            sinks.append(bazel.build_events.grpc(
                uri = bessie_endpoint,
                metadata = {} # TODO: how does bessie authenticate?
            ))
        return sinks

    for task in ctx.tasks:
        if task.name == "build" and task.path.endswith("aspect/build.axl"):
            task.config.startup_flags = lambda f: flags(f, "startup")
            task.config.flags = lambda f: flags(f, "build")
            task.config.build_start = lambda: print("+++ :bazel: Building")
            task.config.build_event_sinks = build_event_sinks
        if task.name == "delivery":
            task.config.delivery_start = lambda: print("--- :bazel: Delivery")
