load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//release/homebrew:brew_go_artifacts.bzl", "brew_go_artifacts")
load(":bazelisk_artifacts.bzl", "bazelisk_artifacts")
load(":release.bzl", "multi_platform_binaries", "release")

brew_go_artifacts(
    name = "aspect_cli_brew_artifacts",
    additional_bins = [":bazel"],
    # Based on Bazelisk brew formula:
    # https://github.com/Homebrew/homebrew-core/blob/7ee2ad3624ac2364aabf28c8d8ee5efaf8592ef1/Formula/bazelisk.rb
    additional_content = """
  conflicts_with "bazel", because: "aspect replaces the bazel binary"
  conflicts_with "bazelisk", because: "aspect replaces the bazel binary"
""",
    bottle_root_url = "https://github.com/aspect-build/aspect-cli/releases/download/0.0.0-PLACEHOLDER",
    desc = "Correct, fast, usable: choose three",
    formula_name = "aspect",
    homepage = "https://aspect.build",
    license = "Apache-2.0",
    multi_platform_go_binaries = "//release:aspect",
    root_files = [
        ":LICENSE",
        ":README.md",
    ],
    url = "https://github.com/aspect-build",
    version_file = "//release:aspect_version_file",
)

multi_platform_binaries(
    name = "aspect",
    embed = ["//cmd/aspect:aspect_lib"],
    tags = ["manual"],
)

expand_template(
    name = "aspect_version_file",
    out = "aspect_version",
    stamp_substitutions = {
        "0.0.0-PLACEHOLDER": "{{STABLE_ASPECT_CLI_BAZELISK_COMPAT_VERSION}}",
    },
    template = ["0.0.0-PLACEHOLDER"],
)

platform_transition_filegroup(
    name = "aspect-windows-x86_64.exe",
    srcs = ["//cmd/aspect"],
    tags = ["manual"],
    target_compatible_with = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
    target_platform = "@zig_sdk//platform:windows_x86_64",
)

bazelisk_artifacts(
    name = "windows_artifacts",
    tags = ["manual"],
    version_file = ":aspect_version",
    windows_x86_64 = ":aspect-windows-x86_64.exe",
)

bazelisk_artifacts(
    name = "linux_and_darwin_artifacts",
    darwin_arm64 = ":aspect-macos-aarch64",
    darwin_x86_64 = ":aspect-macos-x86_64",
    linux_arm64 = ":aspect-linux-aarch64",
    linux_x86_64 = ":aspect-linux-x86_64",
    tags = ["manual"],
    version_file = ":aspect_version",
)

release(
    name = "release",
    tags = ["manual"],
    targets = [
        ":aspect",
        ":linux_and_darwin_artifacts",
        ":aspect_cli_brew_artifacts",
    ],
)

release(
    name = "release.windows",
    tags = ["manual"],
    targets = [
        ":aspect-windows-x86_64.exe",
        ":windows_artifacts",
    ],
)

bzl_library(
    name = "platforms",
    srcs = ["platforms.bzl"],
    visibility = ["//visibility:public"],
)
